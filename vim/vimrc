" Adam Nelson's Global VIMRC
"
" This file contains settings common to all Vim instances
" that I use, on any machine. It should not be used as the
" primary VIMRC file for any Vim instance. Instead, it
" should be sourced from the primary VIMRC, and the primary
" VIMRC can contain any local modifications to the file.
" ---------------------------------------------------------

source ~/etc/vim/plugins.vim

" Basic Options {{{ ---------------------------------------

" sensible.vim should be loaded by Vundle by default, so
" a lot of standard options are missing from here.

" Enable omni completion.
filetype plugin on
set ofu=syntaxcomplete#Complete

" Enable the mouse on the console.
set mouse=a

" Get rid of annoying beeping!
set noerrorbells
set visualbell
set t_vb=
au GUIEnter * set vb t_vb=
set shortmess+=I

" Use spaces instad of tabs. Indent 2 spaces by default.
set expandtab
set shiftwidth=2
set softtabstop=2
set tabstop=8
filetype plugin indent on

" Scroll before the cursor reaches the edge of the window.
set scrolloff=8
set sidescroll=1
set sidescrolloff=15

" Don't close the current buffer when opening a new one.
set hidden

" Substitute globally by default
set gdefault

" Text wrapping
set textwidth=80
set formatoptions=cjqrn1

" Sensible options for omni select
set completeopt=longest,menuone

" Default window dimensions
set winwidth=79
set winheight=5
set winminheight=5

" Set the console window title
set title

" }}} -----------------------------------------------------
" Appearance {{{ ------------------------------------------

" Set the color scheme.
colors jellybeans
set cursorline

" Always show line numbers.
set number
set numberwidth=3

" Highlight search results.
set hlsearch

" Highlight matching brackets.
set showmatch

" No line wrapping for source code.
set nowrap

" HOWEVER, enable line wrapping for plaintext, Markdown, and TeX files!
function! SetWrap()
  setlocal wrap linebreak nolist
  setlocal formatoptions+=t
  nnoremap <Up> gk
  nnoremap <Down> gj
  vnoremap <Up> gk
  vnoremap <Down> gj
endfunction
function! SetNoWrap()
  setlocal nowrap
  setlocal formatoptions-=t
  nnoremap <Up> k
  nnoremap <Down> j
  vnoremap <Up> k
  vnoremap <Down> j
endfunction
autocmd FileType txt call SetWrap()
autocmd FileType markdown call SetWrap()
autocmd FileType tex call SetWrap()
set showbreak=…

" }}} -----------------------------------------------------
" Backup, Swap, and Undo Directories {{{ ------------------

set backup
if exists("+undofile")
  set undofile
end

if has('win32')
  " On Windows, store everything under ~\AppData\Local\Vim

  set shellslash
  if isdirectory($HOME . '\\AppData\\Local\\Vim\\swap') == 0
    :silent !mkdir \%USERPROFILE\%\AppData\Local\Vim\swap
  endif
  if isdirectory($HOME . '\\AppData\\Local\\Vim\\backup') == 0
    :silent !mkdir \%USERPROFILE\%\AppData\Local\Vim\backup
  endif
  if isdirectory($HOME . '\\AppData\\Local\\Vim\\undo') == 0
    :silent !mkdir \%USERPROFILE\%\AppData\Local\Vim\undo
  endif
  set directory=~/AppData/Local/Vim/swap//,c:/tmp//,c:/temp//,.
  set backupdir=~/AppData/Local/Vim/backup//,c:/tmp//,c:/temp//,.
  if exists("+undofile")
    set undodir=~/AppData/Local/Vim/undo//
  endif

else
  " On Linux, store everything under ~/.vim-cache

  if isdirectory($HOME . '/.vim-cache/swap') == 0
    :silent !mkdir -p ~/.vim-cache/swap
  endif
  if isdirectory($HOME . '/.vim-cache/backup') == 0
    :silent !mkdir -p ~/.vim-cache/backup
  endif
  if isdirectory($HOME . '/.vim-cache/undo') == 0
    :silent !mkdir -p ~/.vim-cache/undo
  endif
  set directory=~/.vim-cache/swap//,~/tmp//,/tmp//,.
  set backupdir=~/.vim-cache/backup//,~/tmp//,/tmp//,.
  if exists("+undofile")
    set undodir=~/.vim-cache/undo//
  endif
endif

" }}} -----------------------------------------------------
" Plugin Options {{{ -----------------------------------

" Polyglot
let g:polyglot_disabled = ['haskell', 'typescript']

" Airline
let g:airline_powerline_fonts = 1
let g:airline_theme = 'jellybeans'
let g:airline_skip_empty_sections = 1
let g:airline_inactive_collapse = 1
let g:airline_detect_spelllang = 0
let g:airline_highlighting_cache = 1
let g:airline_section_y = ''

" ALE
let g:ale_sign_error = '✘→'
let g:ale_sign_warning = '!→'

" VimCompletesMe
set completeopt-=preview
autocmd FileType javascript,typescript let b:vcm_tab_complete = 'omni'

" Ctrl-P
let g:ctrlp_use_caching = 0
if executable('ag')
  set grepprg=ag\ --nogroup\ --nocolor
  let g:ctrlp_user_command = 'ag %s -l --nocolor -g "" --ignore *.png --ignore *.jpg --ignore *.zip'
else
  let g:ctrlp_user_command = ['.git',
    \ 'cd %s && git ls-files . -co --exclude-standard',
    \ 'find %s -type f']
endif

let g:ctrlp_match_window_reversed = 0
set wildignore+=*.pyc,*.png,*.jpg,*.gif,*.PNG,*.JPG,*.GIF,*.pdf,*.o,*.obj,.hg
set wildignore+=*/Archive/*,*.jar,*.class,*.doc,*.rtf,*.ppt,*.zip,*.gz,*.bz2
set wildignore+=*.lzma,*.7z,*.exe,*.dll,*.so.*,*~,*.wav,*.mp3,*.flac,*.ogg
set wildignore+=*.ttf,*.mpg,*.avi,*.MPG,*.AVI,*.mp4,*.divx,*.wma,*.wmv
set wildignore+=*.a,*.hi,*/target/*,*/build/*,*/dist/*,*/node_modules/*,*.d.ts

" }}} -----------------------------------------------------
" Haskell {{{ -------------------------------------

function! SetToCabalBuild()
  if glob("*.cabal") != ''
    setl makeprg=cabal\ build
  endif
endfunction

autocmd FileType haskell,lhaskell setl keywordprg=hoogle\ --count=10
"autocmd FileType haskell,lhaskell setl formatprg=xargs\ pointfree
autocmd FileType haskell,lhaskell,cabal call SetToCabalBuild()

command! -range Pf <line1>,<line2>!pointfree "$(cat)"
command! -range Unpf <line1>,<line2>!pointful "$(cat)" | sed 's/^(\(.*\))$/\1/'

function! FindCabalSandboxRoot()
    return finddir('.cabal-sandbox', './;')
endfunction

function! FindCabalSandboxRootPackageConf()
    return glob(FindCabalSandboxRoot().'/*-packages.conf.d')
endfunction

" }}} -----------------------------------------------------
" Misc. Languages {{{ -------------------------------------

function! TypescriptLocalSettings()
  set ballooneval
  setlocal balloonexpr=tsuquyomi#balloonexpr()
  let g:vcm_omni_pattern='.'
  nnoremap <buffer> K :echo tsuquyomi#hint()<CR>
  nnoremap <buffer> <localleader>q :TsuQuickFix<CR>
  nnoremap <buffer> <localleader>d :TsuDefinition<CR>
  nnoremap <buffer> <localleader>r :TsuRenameSymbol<CR>
endfunction

au BufRead,BufNewFile *.txt set filetype=txt
au BufRead,BufNewFile *.md set filetype=markdown
au BufRead,BufNewFile *.gradle set filetype=gradle
au BufRead,BufNewFile *.sig set filetype=sml
au BufRead,BufNewFile *?Script.sml source ~/etc/vim/hol4/hol.vim
autocmd FileType java set tw=100
autocmd FileType scala set tw=100
autocmd FileType json setl formatprg=jq\ .\ -
autocmd FileType typescript call TypescriptLocalSettings()

let g:tex_flavor='latex'

" }}} -----------------------------------------------------
" Key Mappings {{{ ----------------------------------------

let mapleader = '\'
let maplocalleader = ' '

" Shortcuts for plugin commands.
let g:ctrlp_map = '<Leader>p'
nnoremap <Leader><Leader> :CtrlPBuffer<CR>
nnoremap <Leader>t :CtrlPTags<CR>
nnoremap <Leader>n :NERDTree<CR>
nnoremap <Leader>r :RainbowParenthesesToggleAll<CR>
nnoremap <Leader>/ :CtrlPLine<CR>
nnoremap <Leader>a :Ag
nnoremap <Leader>q gqip
nnoremap <Leader>w <C-w>v<C-w>l
set pastetoggle=<Leader>v
au FileType haskell,lhaskell nnoremap <buffer> <leader>ht :HdevtoolsType<CR>
au FileType haskell,lhaskell nnoremap <buffer> <leader>hi :HdevtoolsInfo<CR>
au FileType haskell,lhaskell nnoremap <buffer> <silent> <leader>hc :HdevtoolsClear<CR>

" Open VIMRC with one command.
nnoremap <Leader>rc  :e ~/etc/vim/vimrc<CR>

" Source a modified VIMRC with one command as well.
nnoremap <Leader>src :so ~/etc/vim/vimrc<CR>

" Open help in a vertical split with :vhelp.
cabbrev vhelp botright vert h

" Fix the arrow keys bug in visual mode.
vnoremap <Left>  h
vnoremap <Right> l
vnoremap <Up>    k
vnoremap <Down>  j

" Map CTRL+arrow keys and CTRL+hjkl to switch windows.
nnoremap <C-Left>  <C-w>h
nnoremap <C-Down>  <C-w>j
nnoremap <C-Up>    <C-w>k
nnoremap <C-Right> <C-w>l
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" Use \x to close the current buffer without closing the window.
nnoremap <Leader>x :bp\|bd #<CR>

" Save, compile, and display errors with \m.
nnoremap <Leader>m :w\|silent! make\|cw<CR><C-l>

" Unset the 'last search pattern' register by hitting return.
nnoremap <CR> :noh<CR><CR>

" When searching, center on the next search term.
nnoremap n nzz
nnoremap N Nzz

" Space to replace words!
" c-Space: Change current word.
" d-Space: Delete current word.
" y-Space: Copy current word.
nnoremap c<Space> ciw
nnoremap d<Space> diw
nnoremap y<Space> yiw

" Map semicolon to colon, to reduce finger strain.
" The (unused) comma key replaces the semicolon.
noremap ; :
noremap , ;

" Quit accidentally hitting F1!
inoremap <F1> <ESC>
nnoremap <F1> <ESC>
vnoremap <F1> <ESC>

" CTRL-W shortcuts to match my tmux CTRL-A ones
noremap <C-w>- <C-w>s
noremap <C-w>\| <C-w>v

" Show omni completion with CTRL-Space
inoremap <C-Space> <C-x><C-o>
inoremap <C-@> <C-x><C-o>

" Select from the completion menu with Enter
inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"

" Visually select the text that was last edited/pasted (Vimcast#26).
noremap gV `[v`]

" Y yanks from the cursor to the end of line as expected.
nnoremap Y y$

" Make sure pasting in visual mode doesn't replace paste buffer
function! RestoreRegister()
  let @" = s:restore_reg
  return ''
endfunction
function! s:Repl()
  let s:restore_reg = @"
  return "p@=RestoreRegister()\<cr>"
endfunction
vmap <silent> <expr> p <sid>Repl()

" Forgot-to-unshift versions of :q, :qa, :w, :wq, :wa
command Q :q
command Qa :qa
command W :w
command Wq :wq
command Wa :wa

" }}} -----------------------------------------------------

" vim: foldmethod=marker

